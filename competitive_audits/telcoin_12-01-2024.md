# Telcoin

Telcoin creates low-cost, high-quality financial products for every mobile phone user in the world.

## Findings summary

| ID | Description | Severity |
| --- | --- | --- |
| [H-01](#h-01---wrong-parameter-when-retrieving-causes-a-complete-dos-of-the-protocol) | Wrong parameter when retrieving causes a complete DoS of the protocol | High |
| [H-02](#h-02---the-current-burning-logic-is-flawed) | The current burning logic is flawed | High |
| [H-03](#h-03---denial-of-service-in-mint-after-burning-an-nft) | Denial of service in mint after burning an NFT | High |
| [M-01](#m-01---functions-calling-_retrieve-more-than-once-will-always-revert) | Function calling `_retrieve()` more than once will always revert | Medium |

## Detailed findings

## [H-01] - Wrong parameter when retrieving causes a complete DoS of the protocol

### Bug description

The `CouncilMember` contract is designed to interact with a Sablier stream. As time passes, the Sablier stream will unlock more TELCOIN tokens which will be available to be retrieved from `CouncilMember`.

The `_retrieve()` internal function will be used in order to fetch the rewards from the stream and distribute them among the Council Member NFT holders (snippet reduced for simplicity):

```solidity
// CouncilMember.sol

function _retrieve() internal {
        ...
        // Execute the withdrawal from the _target, which might be a Sablier stream or another protocol
        _stream.execute(
            _target,
            abi.encodeWithSelector(
                ISablierV2ProxyTarget.withdrawMax.selector, 
                _target, 
                _id,
                address(this)
            )
        );

        ...
    }
```

The most important part in `_retrieve()` regarding the vulnerability that we’ll dive into is the  `_stream.execute()` interaction and the params it receives. In order to understand such interaction, we first need understand the importance of the `_stream` and the `_target` variables.

Sablier allows developers to integrate Sablier via [Periphery contracts](https://github.com/sablier-labs/v2-periphery/tree/main), which prevents devs from dealing with the complexity of directly integrating Sablier’s [Core contracts](https://github.com/sablier-labs/v2-core). Telcoin developers have decided to use these periphery contracts. Concretely, the following contracts have been used:

- [ProxyTarget](https://github.com/sablier-labs/v2-periphery/blob/ba3926d2c3e059a230211077087b73afe46acf64/src/SablierV2ProxyTargetApprove.sol) (link points to an older commit because **[the proxy target contracts have now been deprecated from Sablier](https://github.com/sablier-labs/v2-periphery/pull/226)**): stored in the `_target` variable, this contract acts as the target for a PRBProxy contract. It contains all the complex interactions with the underlying stream. Concretely, Telcoin uses the `[withdrawMax()](https://github.com/sablier-labs/v2-periphery/blob/ba3926d2c3e059a230211077087b73afe46acf64/src/abstracts/SablierV2ProxyTarget.sol#L141C5-L143C6)` function in the proxy target to withdraw all the available funds from the stream (as seen in the previous code snippet).
- PRBProxy: stored in the `_stream` variable, this contract acts as a forwarding (non-upgradable) proxy, acting as a smart wallet that enables multiple contract calls within a single transaction.

> NOTE: It is important to understand that the actual lockup linear stream will be deployed as well. The difference is that the Telcoin protocol  will not interact with that contract directly. Instead, the PRBProxy and proxy target contracts will be leveraged to perform such interactions.
> 

Knowing this, we can now move on to explaining Telcoin’s approach to withdrawing the available tokens from the stream. As seen in the code snippet above, the `_retrieve()` function will perform two steps to actually perform a withdraw from the stream:

It will first call the `_stream`'s  `execute()` function (remember `_stream` is a PRBProxy). This function receives a `target` and some `data` as parameter, and performs a delegatecall aiming at the `target`:

```solidity
// https://github.com/PaulRBerg/prb-proxy/blob/main/src/PRBProxy.sol

/// @inheritdoc IPRBProxy
   function execute(address target, bytes calldata data) external payable override returns (bytes memory response) {
        ...

        // Delegate call to the target contract, and handle the response.
        response = _execute(target, data);
    }

    /*//////////////////////////////////////////////////////////////////////////
                          INTERNAL NON-CONSTANT FUNCTIONS
    //////////////////////////////////////////////////////////////////////////*/

    /// @notice Executes a DELEGATECALL to the provided target with the provided data.
    /// @dev Shared logic between the constructor and the `execute` function.
    function _execute(address target, bytes memory data) internal returns (bytes memory response) {
        // Check that the target is a contract.
        if (target.code.length == 0) {
            revert PRBProxy_TargetNotContract(target);
        }

        // Delegate call to the target contract.
        bool success;
        (success, response) = target.delegatecall(data);

        ...
    }
```

In the `_retrieve()` function, the target where the call will be forwarded to is the `_target` parameter, which is a [ProxyTarget](https://github.com/sablier-labs/v2-periphery/blob/ba3926d2c3e059a230211077087b73afe46acf64/src/SablierV2ProxyTargetApprove.sol) contract. Concretely, the delegatecall function that will be triggered in the [ProxyTarget](https://github.com/sablier-labs/v2-periphery/blob/ba3926d2c3e059a230211077087b73afe46acf64/src/SablierV2ProxyTargetApprove.sol) will be `withdrawMax()`:

```solidity
// https://github.com/sablier-labs/v2-periphery/blob/ba3926d2c3e059a230211077087b73afe46acf64/src/abstracts/SablierV2ProxyTarget.sol#L141C5-L143C6

function withdrawMax(ISablierV2Lockup lockup, uint256 streamId, address to) external onlyDelegateCall {
	lockup.withdrawMax(streamId, to);
}
```

As we can see, the `withdrawMax()` function has as parameters the `lockup` stream contract to withdraw from, the `streamId` and the address `to` which will receive the available funds from the stream. The vulnerability lies in the parameters passed when calling the `withdrawMax()` function in `_retrieve()`. As we can see, the first encoded parameter in the `encodeWithSelector()` call after the selector is the `_target`:

```solidity
// CouncilMember.sol

function _retrieve() internal {
        ...
        // Execute the withdrawal from the _target, which might be a Sablier stream or another protocol
        _stream.execute(
            _target,
            abi.encodeWithSelector(
                ISablierV2ProxyTarget.withdrawMax.selector, 
                _target,   // <------- This is incorrect
                _id,
                address(this)
            )
        );

        ...
    }
```

This means that the proxy target’s `withdrawMax()` function will be triggered with the `_target` contract as the `lockup` parameter, which is incorrect. This will make all calls eventually execute `withdrawMax()` on the PRBProxy contract, always reverting.

The parameter needed to perform the `withdrawMax()` call correctly is the actual Sablier lockup contract, which is currently not stored in the `CouncilMember` contract.

### Impact

High. ALL withdrawals from the Sablier stream will revert, effectively causing a DoS in the `_retrieve()` function. Because the `_retrieve()` function is called in all the main protocol functions, this vulnerability essentially prevents the protocol from ever functioning correctly.

### Proof of Concept

Because the current Telcoin repo does not include actual tests with the real Sablier contracts (instead, a `TestStream` contract is used, which has led to not unveiling this vulnerability), [I’ve created a repository](https://github.com/0xadrii/telcoin-proof-of-concept) where the poc can be executed (the repository will be public after the audit finishes (on 15 jan. 2024 at 16:00 CET)). The `testPoc()` function  shows how any interaction (in this case, a call to the `mint()` function) will fail because the proper Sablier contracts are used (PRBProxy and proxy target):

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console2} from "forge-std/Test.sol";
import {SablierV2Comptroller} from "@sablier/v2-core/src/SablierV2Comptroller.sol";
import {SablierV2NFTDescriptor} from "@sablier/v2-core/src/SablierV2NFTDescriptor.sol";
import {SablierV2LockupLinear} from "@sablier/v2-core/src/SablierV2LockupLinear.sol";
import {ISablierV2Comptroller} from "@sablier/v2-core/src/interfaces/ISablierV2Comptroller.sol";
import {ISablierV2NFTDescriptor} from "@sablier/v2-core/src/interfaces/ISablierV2NFTDescriptor.sol";
import {ISablierV2LockupLinear} from "@sablier/v2-core/src/interfaces/ISablierV2LockupLinear.sol";

import {CouncilMember, IPRBProxy} from "../src/core/CouncilMember.sol";
import {TestTelcoin} from "./mock/TestTelcoin.sol";
import {MockProxyTarget} from "./mock/MockProxyTarget.sol";
import {PRBProxy} from "./mock/MockPRBProxy.sol";
import {PRBProxyRegistry} from "./mock/MockPRBProxyRegistry.sol";

import {UD60x18} from "@prb/math/src/UD60x18.sol";
import {LockupLinear, Broker, IERC20} from "@sablier/v2-core/src/types/DataTypes.sol";
import {IERC20 as IERC20OZ} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract PocTest is Test {

    ////////////////////////////////////////////////////////////////
    //                        CONSTANTS                           //
    ////////////////////////////////////////////////////////////////

   bytes32 public constant GOVERNANCE_COUNCIL_ROLE =
        keccak256("GOVERNANCE_COUNCIL_ROLE");
    bytes32 public constant SUPPORT_ROLE = keccak256("SUPPORT_ROLE");

    ////////////////////////////////////////////////////////////////
    //                         STORAGE                            //
    ////////////////////////////////////////////////////////////////

    /// @notice Poc Users
    address public sablierAdmin;
    address public user;

    /// @notice Sablier contracts
    SablierV2Comptroller public comptroller;
    SablierV2NFTDescriptor public nftDescriptor;
    SablierV2LockupLinear public lockupLinear;

    /// @notice Telcoin contracts
    PRBProxyRegistry public proxyRegistry;
    PRBProxy public stream;
    MockProxyTarget public target;
    CouncilMember public councilMember;
    TestTelcoin public telcoin;

    function setUp() public {
        // Setup users
        _setupUsers();

        // Deploy token
        telcoin = new TestTelcoin(address(this));

        // Deploy Sablier 
        _deploySablier();

        // Deploy council member
        councilMember = new CouncilMember();

        // Setup stream
        _setupStream();

        // Setup the council member
        _setupCouncilMember();
    }

    function testPoc() public {
      // Step 1: Mint council NFT to user
      councilMember.mint(user);
      assertEq(councilMember.balanceOf(user), 1);

      // Step 2: Forward time 1 days
      vm.warp(block.timestamp + 1 days);
      
      // Step 3: All functions calling _retrieve() (mint(), burn(), removeFromOffice()) will fail
      vm.expectRevert(abi.encodeWithSignature("PRBProxy_ExecutionReverted()")); 
      councilMember.mint(user);
    }

    function _setupUsers() internal {
        sablierAdmin = makeAddr("sablierAdmin");
        user = makeAddr("user");
    }

    function _deploySablier() internal {
        // Deploy protocol
        comptroller = new SablierV2Comptroller(sablierAdmin);
        nftDescriptor = new SablierV2NFTDescriptor();
        lockupLinear = new SablierV2LockupLinear(
            sablierAdmin,
            ISablierV2Comptroller(address(comptroller)),
            ISablierV2NFTDescriptor(address(nftDescriptor))
        );
    }

    function _setupStream() internal {

        // Deploy proxies
        proxyRegistry = new PRBProxyRegistry();
        stream = PRBProxy(payable(address(proxyRegistry.deploy())));
        target = new MockProxyTarget();

        // Setup stream
        LockupLinear.Durations memory durations = LockupLinear.Durations({
            cliff: 0,
            total: 1 weeks
        });

        UD60x18 fee = UD60x18.wrap(0);

        Broker memory broker = Broker({account: address(0), fee: fee});
        LockupLinear.CreateWithDurations memory params = LockupLinear
            .CreateWithDurations({
                sender: address(this),
                recipient: address(stream),
                totalAmount: 100e18,
                asset: IERC20(address(telcoin)),
                cancelable: false,
                transferable: false,
                durations: durations,
                broker: broker
            });

        bytes memory data = abi.encodeWithSelector(target.createWithDurations.selector, address(lockupLinear), params, "");

        // Create the stream through the PRBProxy
        telcoin.approve(address(stream), type(uint256).max);
        bytes memory response = stream.execute(address(target), data);
        assertEq(lockupLinear.ownerOf(1), address(stream));
    }

    function _setupCouncilMember() internal {
      // Initialize
      councilMember.initialize(
            IERC20OZ(address(telcoin)),
            "Test Council",
            "TC",
            IPRBProxy(address(stream)), // stream_
            address(target), // target_
            1, // id_
            address(lockupLinear)
        );

        // Grant roles
        councilMember.grantRole(GOVERNANCE_COUNCIL_ROLE, address(this));
        councilMember.grantRole(SUPPORT_ROLE, address(this));
    }
  
}
```

### Recommended Mitigation

In order to fix the vulnerability, the proper address needs to be passed when calling `withdrawMax()`. 

> Note that the actual stream address is currently NOT stored in `CouncilMember.sol`, so it will need to be stored (my example shows a new `actualStream` variable)
> 

```diff
function _retrieve() internal {
        ...
        // Execute the withdrawal from the _target, which might be a Sablier stream or another protocol
        _stream.execute(
            _target,
            abi.encodeWithSelector(
                ISablierV2ProxyTarget.withdrawMax.selector, 
-                _target, 
+				 actualStream
                _id,
                address(this)
            )
        );

        ...
    }
```

## [H-02] - The current burning logic is flawed

### Bug description

In order to burn Council NFTs, the `burn()` function in the Council Member contract is used. This function contains a bug:

```solidity
// CouncilMember.sol

function burn(
        uint256 tokenId,
        address recipient
    ) external onlyRole(GOVERNANCE_COUNCIL_ROLE) {
        
				...

        uint256 balance = balances[balances.length - 1];
        balances[tokenId] = balance; 
        balances.pop();
        _burn(tokenId);
    }
```

The previous code snippet has been reduced to show the exact part where `burn()` implements a wrong logic that will make the owner of the last minted token ID unable to access its NFT and capabilities (such as claiming their entitled rewards). 

As we can see in the code snippet, the `burn()` function will

1. Fetch the balance of the last tokenID stored in the `balances` array
2. Set such balance to the position of the `tokenId` that we’re currently burning (in `balances[tokenId]`)
3. Pop the last item in the `balances` array

This logic improperly removes the `balances` stored for the latest token ID, instead of actually removing the balance of the NFT that is currently being burnt. 

Consider the following example, where 4 tokens have been minted with tokenId 0, 1, 2 and 3. All of them have a balance in the `balances` array:

```
tokenId 0 ---> balances[0] => 1 TELCOIN
tokenId 1 ---> balances[1] => 20 TELCOIN     
tokenId 2 ---> balances[2] => 1.6 TELCOIN
tokenId 3 ---> balances[3] => 2.8 TELCOIN  
```

Let’s say that now we want to burn the NFT with token ID 1:

```
tokenId 0 ---> balances[0] => 1 TELCOIN
tokenId 1 ---> balances[1] => 20 TELCOIN     
tokenId 2 ---> balances[2] => 1.6 TELCOIN
tokenId 3 ---> balances[3] => 2.8 TELCOIN  
```

With the current logic:

1. `balance` will store `balances[balances.length - 1]` , which is the last token Id’s balance (2.8 TELCOIN)
2. `balances[1]` will be updated to store the value saved in the `balance` variable (2.8 TELCOIN). 
3. Finally, the last element in `balances` will be popped, making position 3 in the `balances` array not exist, and preventing the owner of tokenID 3 to never be able to access its token balance:

```
After burning:
tokenId 0 ---> balances[0] => 1 TELCOIN
tokenId 1 ---> balances[1] => 2.8 TELCOIN  <--- (burnt)  
tokenId 2 ---> balances[2] => 1.6 TELCOIN
tokenId 3 ---> balances[3] => ?            <--- just popped
```

Because `balances` positions are expected to be “linked” to the tokenId number, burning an NFT will lead all the subsequent NFT mintings to work improperly. 

Following the previous example, if a new NFT with tokenID 4 is minted, a new element will be pushed to the `balances` array. However, the balances array only had three elements due to burning tokenID 1, so the new item pushed to the `balances` array will be stored in position 3 of the array. At this point, owner of tokenID 3 will regain access to the `balances[3]` position, but the new owner of tokenID 4 will not have a position in the `balances` array.

### Impact

Medium. Balances of the last tokenID will be completely lost and unaccessible.

### Proof of Concept

The following proof of concept shows how the previous vulnerability affects any interaction with the `balances` array. In order to run it, add the following snippet of code in the `CouncilMember.test.ts` file, and execute the following command: `npx hardhat test test/sablier/CouncilMember.test.ts --grep Vulnerability` 

```tsx
it("Vulnerability: burning prevents last tokenID owner to interact with the contract ", async () => {
		// Step 1: Burn tokenId with ID 1
		await expect(councilMember.burn(1, support.address)).emit(councilMember, "Transfer");
		// Step 2: Verify that even that tokenId 1 was burnt, tokenId 2 still exists and has an owner
		const tokenIdTwoOwner = await councilMember.ownerOf(2);
		await expect(tokenIdTwoOwner).to.be.equal(await stream.getAddress())
		// Step 3: The vulnerability prevents any access to the last position from succeeding due to being out of bounds
		// for the last tokenId
		await expect(councilMember.balances(2)).to.be.revertedWithoutReason();
    });
```

### Recommended Mitigation

Currently, the protocol expects tokenIds to be bound to a position in the `balances` array due to how `_retrieve()` distributes the stream’s funds among users.  It is encouraged to rethink the approach by which balances are tracked so that such issue doesn’t arise when burning, and the corresponding balances for each holder are not affected by reducing the `balances` array.

## [H-03] -  Denial of service in mint() after burning an NFT

### Bug description

The `CouncilMember` ’s `mint()` function allows creating new council member NFTs. In order to do so, the ERC721’s internal `_mint()` function is called, passing the `newMember` as the receiver address, and the `totalSupply()` as the tokenID to mint:

```tsx
// CouncilMember.sol

function mint(
        address newMember
    ) external onlyRole(GOVERNANCE_COUNCIL_ROLE) {
        if (totalSupply() != 0) {
            _retrieve();
        }

        balances.push(0);
        _mint(newMember, totalSupply());
    }
```

The problem with using the `totalSupply()` as the new token ID is that the function will no longer work if an NFT is burnt.

Consider the following scenario, where four NFTs exist (`totalSupply()` is four), with tokenIds 0, 1, 2 and 3.

- Governance decides to burn the NFT with tokenId 2. This will make `totalSupply()` decrease to 3.
- After burning, the current existing NFTs are the ones with tokenId 0, 1 and 3
- Some time after burning the NFT, a new mint is required. Because `totalSupply()` has decreased to 3, `mint()` will try to mint the NFT with tokenId 3. However, the NFT with tokenId already exists, so this function will always fail, effectively making it completely impossible to mint NFTs.

### Impact

ll the calls to the `mint()` function will fail after any `burn()` is performed. Because minting is a critical protocol function, the impact of this vulnerability is high.

### Proof of Concept

The following proof of concept illustrates the issue. In order to run it, paste the code in the `CouncilMember.test.ts` file, and execute the following command in your terminal: `npx hardhat test test/sablier/CouncilMember.test.ts --grep Vulnerability`

```tsx
it("Vulnerability: Using totalSupply() as tokenId to mint leads to DoS after any token is burnt", async () => {
      // Step 1: Burn NFT with tokenId 1
      await expect(councilMember.burn(1, support.address)).emit(councilMember, "Transfer");
      // Step 2: Confirm that totalSupply() has decreased from 3 to 2
      const totalSupplyAfterBurning = await councilMember.totalSupply()
      await expect(totalSupplyAfterBurning).to.be.eq(2);

      // Step 3: Burning the NFT has decreased totalSupply from 3 to 2. Hence, the next tokenId that will be minted
      // is the NFT with tokenId 2. Because tokenId 2 already exists, minting will always revert
      // with error ERC721InvalidSender()
      await expect(councilMember.mint(holder.address)).to.be.revertedWithCustomError(councilMember, "ERC721InvalidSender")
  });
```

### Recommended Mitigation

Consider rethinking the approach in which NFTs are minted/tracked. A common decision is to have an internal, increment-only variable that will never be decreased even if new NFTs are minted.

## [M-01] -  Functions calling _retrieve() more than once will always revert

### Bug description

`CouncilMember.sol` uses the `_retrieve()` function in order to withdraw the current available amount in the Sablier stream.

In order to do so, the `_retrieve()` will call `withdrawMax()` in the proxy target, a contract which will internally trigger the stream’s `withdrawMax()` function:

```solidity
// CouncilMember.sol

function _retrieve() internal {
        // Get the initial TELCOIN balance of the contract
        uint256 initialBalance = TELCOIN.balanceOf(address(this));
        // Execute the withdrawal from the _target, which might be a Sablier stream or another protocol
        _stream.execute(
            _target,
            abi.encodeWithSelector(
                ISablierV2ProxyTarget.withdrawMax.selector, 
                _target, 
                _id,
                address(this)
            )
        );

        ...
    }

```

```solidity
// https://github.com/sablier-labs/v2-periphery/blob/ba3926d2c3e059a230211077087b73afe46acf64/src/abstracts/SablierV2ProxyTarget.sol#L142
// SablierV2ProxyTarget.sol
function withdrawMax(ISablierV2Lockup lockup, uint256 streamId, address to) external onlyDelegateCall {
        lockup.withdrawMax(streamId, to);
    }
```

When `lockup.withdrawMax()`, the actual Sablier lockup linear stream’s `withdrawMax()` will be called. This function will withdraw the maximum withdrawable amount available in the stream, which is given by the internal `_withdrawableAmountOf()` function:

```solidity
// https://github.com/sablier-labs/v2-core/blob/main/src/abstracts/SablierV2Lockup.sol#L298

function withdrawMax(uint256 streamId, address to) external override {
        withdraw({ streamId: streamId, to: to, amount: _withdrawableAmountOf(streamId) });
    }
```

As we can see in the snippet, `withdrawMax()` will then execute `withdraw()`, the actual stream’s function that will carry out the logic to transfer the available amount in the stream. If we analyze the `withdraw()` function, we’ll notice that [it will always revert if a 0 amount is passed as parameter](https://github.com/sablier-labs/v2-core/blob/main/src/abstracts/SablierV2Lockup.sol#L270).

The problem with the current Telcoin implementation: because some functions call the `_retrieve()` function twice, all of them will fail because the first call to `_retrieve()` will withdraw ALL the available funds. After performing the first retrieve, the second one will always try to withdraw a 0 amount because everything has been withdrawn in the first retrieval.

This affects the `removeFromOffice()`, `mint()`and `burn()` functions. All of them perform only one explicit call to `_retrieve()`, but are then also affected by the changes added by the Telcoin team in the ERC721’s `_update()` hook:

```solidity
// CouncilMember.sol
function _update(
        address to,
        uint256 tokenId,
        address auth
    ) internal override returns (address) {
        if (totalSupply() != 0) {
            _retrieve();
        }

        return super._update(to, tokenId, auth);
    }
```

This hook forces all NFT minting, burning or transfers to trigger a `_retrieve()`, which are actions performed by the three mentioned functions.

### Impact

High. Calling the `removeFromOffice()`, `mint()`and `burn()` functions after `totalSupply()` > 1 (i.e after the first NFT is minted) will always revert, effectively rendering the protocol unusable.

### Recommended Mitigation

It is recommended to only execute the `_retrieve()` if the stream’s withdrawable amount is greater than 0 (i.e there’s something to withdraw). This can be easily queried in [the stream's `withdrawableAmountOf()` function](https://github.com/sablier-labs/v2-core/blob/main/src/abstracts/SablierV2Lockup.sol#L117).