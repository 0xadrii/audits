# [0xadrii] Bug disclosure - AmmPoolsServiceEth is vulnerable to inflation attacks

## Target

https://etherscan.io/address/0xA30845738443Aa2dd6bd0783A47B0AF8C01A9BED

## Vulnerability

The AmmPoolsServiceEth offers liquidity providers with the capability of providing liquidity in the form of ETH, WETH and stETH. When a user deposits liquidity, assets will be transferred to the AMM Treasury ETH contract, and a corresponding amount of shares in the form of ipTokens (in this specific case, ipstETH) will be minted to the deposit beneficiary.

The process of computing the corresponding amount of shares that should be minted is similar to the computations performed in an ERC4626 vault.
```solidity
// AmmPoolsServiceEth.sol
function provideLiquidityStEth(address beneficiary, uint256 stEthAmount) external payable override {
        StorageLib.AmmPoolsParamsValue memory ammPoolsParamsCfg = AmmConfigurationManager.getAmmPoolsParams(stEth);
				
				// Step 1 
        uint256 newPoolBalance = stEthAmount + IStETH(stEth).balanceOf(ammTreasuryEth);

        require(
            newPoolBalance <= uint256(ammPoolsParamsCfg.maxLiquidityPoolBalance) * 1e18,
            AmmErrors.LIQUIDITY_POOL_BALANCE_IS_TOO_HIGH
        );
				
				// Step 2
        uint256 exchangeRate = AmmLibEth.getExchangeRate(stEth, ipstEth, ammTreasuryEth);
				 
				// Step 3
        IStETH(stEth).safeTransferFrom(msg.sender, ammTreasuryEth, stEthAmount);

        uint256 ipTokenAmount = IporMath.division(stEthAmount * 1e18, exchangeRate);
 
        IIpToken(ipstEth).mint(beneficiary, ipTokenAmount); 

        emit IAmmPoolsServiceEth.ProvideLiquidityStEth(
            msg.sender,
            beneficiary,
            ammTreasuryEth,
            exchangeRate,
            stEthAmount,
            ipTokenAmount
        );
    }
```

The previous code snippet [extracted from this address](https://etherscan.io/address/0xA30845738443Aa2dd6bd0783A47B0AF8C01A9BED) performs the following steps:

1. Initially, it will compute newPoolBalance by adding the stEthAmount willing to be deposited by the user + the current stEth balance of ammTreasuryEth. This is done so that a check against the maximum maxLiquidityPoolBalance allowed is performed, so that users don’t exceed such limit.
2. After ensuring maxLiquidityPoolBalance is not reached, the exchange rate between stEth and ipstETH is computed. Essentially, this will help us know how many stEth is one ipstEth worth (and viceversa). We can see that the exchange rate is obtained by calling the getExchangeRate() function in the AmmLibEth library, which performs the following calculations:

```solidity
// AmmLibEth.sol
function getExchangeRate(address stEth, address ipstEth, address ammTreasuryEth) internal view returns (uint256) {
        uint256 ipTokenTotalSupply = IIpToken(ipstEth).totalSupply();

        if (ipTokenTotalSupply > 0) {
            return IporMath.division(IStETH(stEth).balanceOf(ammTreasuryEth) * 1e18, ipTokenTotalSupply); 
        } else {
            return 1e18;
        }
    }
```

If the ipTokenTotalSupply is 0, the exchange rate is hardcoded at 1e18 (this makes sense, given that nobody has yet deposited and hence 1 ipstEth = 1 stEth). On the other hand, if ipTokenTotalSupply > 0 , the rate will be computed by performing a division between the current stEth balance of the ammTreasuryEth and the current ipTokenTotalSupply . Here lies the issue. Because getExchangeRate() directly queries the ammTreasuryEth balance via stEth’s balanceOf() function, this computation is subject to the well-known inflation attack commonly found in ERC4626 vaults. Querying the balance is the entry point for this whole attack.

>Note: It is important to highlight the fact that this issue does NOT occur in the regular AmmPoolsService contract (which allows providing liquidity for USDT, DAI AND USDC). We can see how the regular AmmPoolsService will query AmmLib.getExchangeRate() in order to obtain the exchange rate, a function where the balance used to compute the exchange rate is not obtained from directly querying the balanceOf() function of the token, but rather by tracking balances internally with the help of the liquidityPool variable that gets stored in the AmmBalancesMemory struct):

```solidity
// AmmLib.sol 
function getExchangeRate(AmmTypes.AmmPoolCoreModel memory model) internal view returns (uint256) {
        (, , int256 soap) = getSoap(model); 

        uint256 liquidityPoolBalance = getAccruedBalance(model).liquidityPool; // <--- Liquidity pool balance is extracted from a variable that tracks liquidity internally
 
        int256 balance = liquidityPoolBalance.toInt256() - soap;

        require(balance >= 0, AmmErrors.SOAP_AND_LP_BALANCE_SUM_IS_TOO_LOW);

        uint256 ipTokenTotalSupply = IIpToken(model.ipToken).totalSupply();

        if (ipTokenTotalSupply > 0) {
            return IporMath.division(balance.toUint256() * 1e18, ipTokenTotalSupply); // <--- `balance` comes from querying a variable that tracks liquidity internally, rather than queryint the token's balanceOf() function
        } else {
            return 1e18;
        }
    }
```

3. After computing the exchange rate, stEth will be transferred from the depositor to the ammTreasuryEth address, and then the wrongly computed exchange rate will be used to calculate the final ipTokenAmount that should be minted to the beneficiary.

Before continuing with the report, I highly recommend the reader to check [this link](https://mixbytes.io/blog/overview-of-the-inflation-attack), where the inflation attack is clearly detailed. After checking the previous article, the reader will notice that roundings can be performed in two flavours: rounding shares to zero or rounding shares to one. Because ipTokens don’t allow minting a 0 amount (IpToken.sol has a check in the mint() function that requires the mint amount to be greater than 0), the only possible option to perform this attack with the current IPOR code is by rounding shares to one, rather than rounding shares to 0. Although this makes the attack less profitable than if we rounded shares to 0, it is worth noting that it is still possible to highly profit from other depositor’s funds, effectively creating a situation where a direct theft of user funds takes place.

The Proof of Concept section of this report details a numerical example where an attacker performs such an attack, effectively stealing funds from a regular liquidity provider and obtaining a 50% return.

## Impact
It is clear that the impact for this kind of vulnerability is critical. As per immunefi’s guidelines: Direct theft of any user funds, whether at-rest or in-motion, other than unclaimed yield will be considered critical. However, it must be highlighted that **the attack can not be currently performed on the mentioned asset**, given that the code is live and several depositors have already provided some liquidity, thus effectively making the attack unfeasible, I have decided to downgrade the impact from Critical to High.

Because IPOR seeks at building a benchmark reference interest rate sourced from other DeFi credit protocols, it is probable that more ETH-like (such as rETH or osETH) assets will be supported. The likeliness of this attack being found in the future with more tokens being supported by IPOR is extremely high. This will make such assets vulnerable to this attack at the moment where they are deployed. Following IPOR’s documentation to the answer of [Will there be other IPOR rates](https://docs.ipor.io/ipor-faq/about-the-ipor-protocol#18.-will-there-be-other-ipor-rates):

“Launching new IPORs beyond USDC, USDT, DAI, and stETH (in the works) must be supported by market demand and liquidity, and will be dependent on a decision by the IPOR DAO to support new assets.”.

If the DAO decides to launch such assets, the same approach as AmmPoolsServiceEth will be used, effectively enabling attackers to performing an inflation attack.

## Proof of concept

The following proof of concept illustrates how the attack described previously could take place:

1. A new AmmPoolsServiceEth contract is deployed to handle another ETH-like asset. A regular user decides to deposit 10e18 stETH to test the IPOR protocol. An attacker sees the user’s transaction, and decides to perform the inflation attack.
2. The attacker front-runs the depositor, depositing 1 wei of stETH into AmmPoolsServiceEth. This makes the getExchangeRate() return 1e18 given the fact that ipTokenTotalSupply is zero, so the final computed ipTokenAmount to be minted is 1 * 1e18 / 1e18 = 1 wei.
3. After depositing 1 wei, the attacker directly transfers 5e18 stETH to the ammTreasuryETH. This is the “inflation”. Now, the current ipToken.totalSupply() is still of 1 wei, but ammTreasuryETH's stETH balance has increased to 5e18 + 1 stETH.
4. After the inflation, the initial depositor’s transaction gets executed, depositing 10e18 stETH into the pool. However, the depositor will ONLY get 1 share:
getExchangeRate() = IStETH(stEth).balanceOf(ammTreasuryEth) * 1e18 / ipTokenTotalSupply, so getExchangeRate() = (5e18 + 1) * 1e18 / 1 wei.
The minted ipTokenAmount computed will then be 10e18 * 1e18 / getExchangeRate(), or in other words, ipTokenAmount = 10e18 * 1e18 / ((5e18 + 1) * 1e18 / 1 wei), which gives a result of 1.9999999999999999996 ≈ 1 (1 because solidity rounds down), giving the depositor exactly 1 wei of shares.
5. Finally, both the attacker and the user have 1 wei of ipTokens shares. Now, total assets held in treasury are (15e18 + 1 stETH). This amount will be distributed pro-rata among the attacker and the depositor, given that each of them has one single wei of minted shares. If the attacker decides to withdraw, he will obtain half of the pooled assets, effectively obtaining back (15e18 + 1 stETH) / 2 = 7.5e18 stETH (note: redeem fees are not considered for simplicity). This makes the attacker obtain a 2.5e18 stETH benefit (attacker deposited 5e18 + 1 stETH and obtained 7.5e18 stETH), or in other words, a 50% benefit from the attack.

It is important to highlight the fact that this attack can be performed with any amounts, given that the attacker will always be capable to obtain a 50% benefit from the attack. The higher the user deposited amount, the higher returns for the attacker.

## Recommendation

There are several ways to mitigate this issue. The most elegant way is to track balances internally just like AmmPoolsService does. This removes the inflation effect due to the fact that tracking balances internally won’t rely on the token’s balanceOf() function, and will make the AmmPoolsServiceEth and AmmPoolsService operate with funds in a similar fashion.

Another possible fix is to create “dead shares”, which means minting a small amount (for example 1000) of IPTokens to the 0 address when the first deposit takes place. This second approach makes the attack unfeasible for the attacker, given that ip tokens total supply increases by several magnitudes, so an extremely huge amount of funds is required in order to perform the attack.