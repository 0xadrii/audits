# [0xadrii] Bug disclosure - DoS in LiquidityMining's `updateIndicators()` and `calculateAccruedRewards()` due to missing loop variable increment


## Target
Primacy of Impact - https://github.com/IPOR-Labs/ipor-power-tokens/blob/main/contracts/mining/LiquidityMining.sol

## Vulnerability
LiquidityMining.sol's updateIndicators() and calculateAccruedRewards() might run out of gas in certain conditions due to a misplacement of the continue keyword, causing a temporary DoS of the functions.

Both updateIndicators() and calculateAccruedRewards() functions contain for loops that are optimized by incrementing the i variable inside an unchecked field. Although this is a good practice in order to optimize gas usage, there are two situations where such functions will run out of gas, due to entering an if condition that contains a continue, which will jump to the next iteration of the loop without incrementing the i loop variable. Concretely, such situations occur when:

lpTokenBalance in accountIndicators of the current account is 0, in updateIndicators() function (https://github.com/IPOR-Labs/ipor-power-tokens/blob/main/contracts/mining/LiquidityMining.sol#L138)
aggregatedPowerUp in globalIndicators is 0, in calculateAccruedRewards() (https://github.com/IPOR-Labs/ipor-power-tokens/blob/main/contracts/mining/LiquidityMining.sol#L71).

```solidity
function updateIndicators( 
        address account,
        address[] calldata lpTokens
    ) external override nonReentrant whenNotPaused {
     
    ...
        uint256 lpTokensLength = lpTokens.length;
        uint256 rewardsAmountToTransfer;
        address lpToken;
        uint256 rewardsAmount;
        uint256 accruedCompMultiplierCumulativePrevBlock;

        for (uint256 i; i != lpTokensLength; ) {
            lpToken = lpTokens[i];

            accountIndicators = _accountIndicators[account][lpToken];
            globalIndicators = _globalIndicators[lpToken];

            if (accountIndicators.lpTokenBalance == 0) {
                continue;
            }

            ...

            unchecked {
                ++i;
            }
        }
        ...
    }

function calculateAccruedRewards(
        address[] calldata lpTokens
    ) external view override returns (LiquidityMiningTypes.AccruedRewardsResult[] memory result) {
        ...

        for (uint256 i; i != lpTokensLength; ) {
            globalIndicators = _globalIndicators[lpTokens[i]];
            if (globalIndicators.aggregatedPowerUp == 0) {
                rewards[i] = LiquidityMiningTypes.AccruedRewardsResult(
                    lpTokens[i],
                    globalIndicators.accruedRewards
                );
                continue;
            }

            ...

            unchecked {
                ++i;
            }
        }
        return rewards;
    }
```

This is however mitigated in the [addPwTokensInternal() function](https://github.com/IPOR-Labs/ipor-power-tokens/blob/main/contracts/mining/LiquidityMining.sol#L286), where even entering an if statement, the i variable is incremented, which is the correct behavior when performing such gas optimizations :

```solidity
function addPwTokensInternal(
        LiquidityMiningTypes.UpdatePwToken[] memory updatePwTokens
    ) external onlyRouter whenNotPaused {
        ...

            /// @dev when account not stake any IP Token then calculation rewards and rebalancing is redundant
            if (accountIndicators.lpTokenBalance == 0) {
                uint256 newBalance = accountIndicators.delegatedPwTokenBalance +
                    update.pwTokenAmount;
                _accountIndicators[update.beneficiary][update.lpToken]
                    .delegatedPwTokenBalance = newBalance.toUint96();
                emit PwTokenDelegated(update.beneficiary, update.lpToken, update.pwTokenAmount);
                unchecked {
                    ++i;
                }
                continue;
            }

            ...
            unchecked {
                ++i;
            }
            ...
    }
```

## Impact

Medium, functions won't be able to execute if wrong due to always running out of gas in case the function enters the mentioned if conditions.

Probability: low, it is easily mitigatable and does not affect critical functions

## Remediation

Increment the i variables inside the mentioned if statements so that the function does not run out of gas.